apply plugin: 'com.android.application'
apply plugin: 'android-apt'

android {
    compileSdkVersion 24
    buildToolsVersion "24.0.1"

    defaultConfig {
        applicationId "com.github.dfa.diaspora_android"
        minSdkVersion 17
        targetSdkVersion 24
        versionCode 7
        versionName "0.1.6-dev"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        disable 'MissingTranslation'
    }

    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.outputFile = new File(
                    output.outputFile.parent, "diasporaAndroid__${variant.versionName}__.apk")
        }
    }
}

dependencies {
    // Sub-Projects
    //compile project(':subprojectFromRoot')

    // Jars
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'

    // Android standard libs
    compile 'com.android.support:appcompat-v7:24.1.1'
    compile 'com.android.support:design:24.1.1'
    compile 'com.android.support:support-v4:24.1.1'

    // More libraries
    compile 'com.jakewharton:butterknife:8.0.1'
    compile 'info.guardianproject.netcipher:netcipher:2.0.0-alpha1'
    compile 'info.guardianproject.netcipher:netcipher-webkit:2.0.0-alpha1'
    apt 'com.jakewharton:butterknife-compiler:8.0.1'
}


task copyRepoFiles(type: Copy) {
    String[] copyFiles = ["README.md", "LICENSE.md", "CHANGELOG.md"]
    from rootProject.files(copyFiles)
    into 'src/main/res/raw'
    rename { String fileName ->
        fileName.replace(fileName, fileName.toLowerCase())
    }

    // Filter Contributors file
    from(rootProject.file("CONTRIBUTORS.md")) {
        into '.'    // Target already changed to 'src/main/res/raw'
        rename { String fileName ->
            fileName.replace(fileName, fileName.toLowerCase())
        }
        filter { line ->
            (line.toString().matches("..l>>.*") || line.toString().startsWith("## 99l CONTRIBUTORS")) ? null : line.toString().trim().replaceAll(" \\(.*\\)", "")
        }
    }

}
tasks
        .
        copyRepoFiles.execute()